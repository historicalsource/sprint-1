	.TITLE RATS
TRKTAB	=26712
OILS	=27326
HOFF	=27406
VOFF	=27446
ATAB2	=27506
ATAB1	=27606
SLSD	=2052
SMSD	=2051
SHSD	=2050
HLSD	=2073
HMSD	=2072
HHSD	=2071
TLSD	=2074
TMSD	=2073
THSD	=2072
PF	=2000
OF	=2200
SBO	=6060
GP	=4053
SST	=4054
SBI	=4055
LFREQ	=4057
GTIME	=4063
SPARE	=4060
GEARAD	=4050
MODE	=4061
BONUS	=4062
COINAD	=4100
STR	=4200
VEL	=0
DIRECT	=4
HPOSL	=10
VPOSL	=14
HPOS	=20
SOUND	=24
VPOS	=30
ROTATE	=31
CREDIT	=40
TEMP1	=42
TEMP2	=44
TEMP3	=46
TEMP4	=50
TEMP5	=52
AUDY	=54
FRAME	=60
FLAGZ	=61
GEAR	=62
FLAGH	=63
FLAGQ	=64
TRKN	=65
COINS	=66
TOGGLE	=67
FLAGP	=70
EXTEND	=72
TCOUNT	=74
FLAGD	=75
FLAGF	=76
TSAVE	=77
HPOST	=100
VPOST	=104
TH1	=110
TH2	=112
TH3	=114
TH4	=116
TSP	=120
CFLAG	=121
LFLAG	=122
BCD1	=124
BCD2	=125
BCD3	=126
HS1	=127
HS2	=130
HS3	=131
LOC	=176
SK	=6020
HIT	=10000
STRES	=7000
CRES	=6400
TREST	=6200
NREST	=7400
RTEST	=141
AMODE	=6000
CRASH	=26
TEST	=14000
TESTV	=252
	.ASECT
.	=30000
NMI:	PHA
	TXA
	PHA
	CLD
	LDA	Z,FRAME
	BNE	S,+5
	STA	A,NREST			;RESET NOISE CIRCUIT
	BIT	A,SST
	BPL	NM1
	LDA	Z,RTEST
	CMP	I,222			;1ST TIME?
	BNE	N1			;YES
	LDA	Z,RTEST-1
	CMP	I,77
	BNE	N1
	TSX
	LDA	AX,405			;GET PCH
	CMP	I,40			;VALID RANGE?
	BCS	NM1
	CMP	I,100
	BCC	NM1
	CMP	I,20
	BCC	PWRON			;NO
	CMP	I,30
	BCS	PWRON
NM1:	PLA
	TAX
	PLA
	RTI				;RETURN
N1:	LDA	I,222			;INITIALIZE RAM LOCATION
	STA	Z,RTEST
	LDA	I,77
	STA	Z,RTEST-1
PWRON:	SEI
	CLD
	LDX	I,177
	TXS				;SET STACK POINTER=7F
	LDA	A,TEST
	CMP	I,TESTV			;DIAGNOSTIC PRESENT?
	BEQ	TESTVJ			;YES, GO TO TEST
	JMP	A,MAINT			;GO TO MAIN LOOP
TESTVJ:	JSR	A,TEST
MAINT:	LDX	I,22.			;BLANK 1ST 2 LINES
	JSR	A,BLANK
	LDX	I,4
	JSR	A,BLANK
	LDX	I,12.
	JSR	A,BLANK
	JSR	A,SETUP			;INITIALIZE EVERYTHING
MAIN:	JSR	A,ATRACT		;GO TO ATTRACT MODE
	JSR	A,START			;START GAME
	JMP	A,MAIN
;
;ATTRACT SUBROUTINE
;
ATRACT:	LDA	I,0
	STA	Z,TRKN
	STA	Z,FLAGP
	STA	A,AMODE+1		;TURN OFF SOUND
AAA:	JSR	A,TRACK			;OUTPUT INITIAL TRACK
	JSR	A,INITIL		;SET UP CARS
	JSR	A,SCR
	JSR 	A,COIN
	LDA	I,1
	LDX	I,30.
	JSR	A,MESS
	LDA	I,0
	LDX	Z,CFLAG
	JSR	A,MESS
	LDA	I,0
	LDX	I,14.
	JSR	A,MESS
	LDA	I,0
	LDX	I,16.
	JSR	A,MESS
A3:	INC	Z,FRAME
	LDA	A,FRAME
	BNE	A5
	INC	Z,TRKN
	LDA	Z,TRKN
	CMP	I,14
	BNE	A5
	LDA	I,0
	STA	Z,TRKN
	STA	Z,FLAGP
	JMP	A,AAA
A5:	BIT	A,SST			;SIMPLE SELF-TEST?
	BMI	S,+5
	JMP	A,SLT
	JSR	A,TRANS
	LDX	I,0			;CAR #
A1:	JSR	A,CNTRL
	INX				;COMPUTER-CONTROL IT
	CPX	I,4
	BNE	A1
	LDA	I,0
	STA	Z,CRASH
	STA	A,SOUND
	STA	A,SK
A2:	BIT	A,COINAD		;CONTINUALLY LOOK
	BPL	A8			;FOR COINS
	BVS	A9
A8:	JSR	A,COIN
A9:	BIT	A,AMODE			;WAIT FOR V RESET
	BVC	A2
	STA	A,TREST
	LDA	Z,COINS			;ANY COINS?
	BNE	A4			;YES
	LDA	Z,CREDIT		;ANY CREDIT?
	BEQ	A3			;NO, CONTINUE ATTRACTING
A4:	JSR	A,LINE
	LDA	Z,COINS
	BNE	S,+6
	LDA	Z,CREDIT
	BEQ	A3
	RTS				;YES, GO TO MAINLINE
;
;START PLAY SUBROUTINE
;
START:	NOP
S1:	LDX	I,0			;OUTPUT 1ST PLAYFIELD
	STX	Z,TRKN
	STX	Z,FLAGP			;OWEN RUBEN PATCH
	JSR	A,TRACK			
	LDX	I,6
	JSR	A,BLANK
	LDA	I,0
	LDX	I,0
	JSR	A,MESS
	LDA	I,0
	STA	Z,TEMP5
	JSR	A,INITIL		;SET-UP CARS
	JSR	A,TRANS
S2:	JSR	A,COIN			;CONTINUE TO LOOK FOR COINS
	STA	A,TREST
	LDA	Z,CREDIT		;ANY CREDIT?
	BEQ	S2			;NO
	STA	A,SBO+1			;TURN ON START BUTTON LIGHT
	LDA	I,0
	LDX	I,20.
	JSR	A,MESS
	BIT	A,SBI			;YES, LOOK FOR START BUTTON
	BMI	S2
	DEC	Z,CREDIT		;DECREMENT CREDIT
	LDX	I,0
	JSR	A,BLANK			;BLANK INSTRUCTIONS
	LDX	I,20.
	JSR	A,BLANK
	LDX	I,18.
	JSR	A,BLANK
	LDA	Z,EXTEND
	CMP	I,100
	BEQ	S205
	LDA	I,0
	LDX	I,18.
	JSR	A,MESS
	LDA	Z,EXTEND
	CMP	I,60
	BEQ	S,+5
	STA	A,3564
	LDA	Z,EXTEND+1
	STA	A,3565
S205:	STA	A,AMODE			;TURN ON SOUND
PLAY:	JSR	A,TRANS
	LDA	Z,FLAGP			;STILL UPDATING TRACK?
	BEQ	S3
	JSR	A,TRACK			;YES, CALL TRACK
S3:	INC	Z,FRAME			;INCREMENT FRAME COUNT
	JSR	A,STEER
	LDX	I,1
S31:	JSR	A,CNTRL			;COMPUTER-CONTROL CARS
	INX
	CPX	I,4
	BNE	S31
;
;USER CAR
;
;   PLAYFIELD COLLISION
;
	STA	A,SK			;TURN OFF SKID
	LDX	I,0
	BIT	A,HIT			;PLAYFIELD HIT?
	BMI	S60			;YES
	LDA	Z,FLAGD			;DISABLE WALL HIT LOGIC
	BEQ	S60			;FOR 16 FRAMES
	DEC	Z,FLAGD
	JMP	A,S70
S60:	BIT	A,HIT			;PLAYFIELD HIT?
	BMI	S61
	LDA	I,0			;NO, CLEAR FLAGS
	STA	Z,FLAGD
	STA	Z,FLAGF
	JMP	A,S70
S61:	LDA	I,0
	STA	Z,FLAGH			;DISABLE CAR HITS
	LDA	Z,FLAGF			;YES, 1ST TIME?
	BNE	S62			;NO
	LDA	I,1			;YES, SET VEL=1
	STA	ZX,VEL
	STA	Z,FLAGF
	LDA	I,17			;SET UP CRASH NOISE
	STA	Z,CRASH
S62:	LDA	I,20
	STA	Z,FLAGD
S70:	LDA	Z,CRASH			;CRASH STILL ACTIVE?
	BEQ	S80			;NO
	LDA	Z,FRAME			;YES, 4TH FRAME?
	LSR
	BCC	S80			;NO
	LSR	
	BCC	S80
	DEC	Z,CRASH			;YES, CONTINUE CRASH
S80:	LDA	Z,FLAGH			;CAR HIT STILL ACTIVE?
	BEQ	S81			;NO
	STA	A,SK+1			;YES, KEEP SKID SOUND ON
	DEC	Z,FLAGH
	TXA
	ASL
	TAY
	LDA	I,2
	BIT	A,STR
	BVS	S800
	LDA	I,376
S800:	CLC
	ADC	AY,ROTATE
	STA	AY,ROTATE
;
;CAR COLLISION
;
S81:	BIT	A,HIT			;CAR HIT?
	BVC	S90			;NO
	LDA	I,40			;YES, SET UP CAR HIT ACTIVE
	STA	Z,FLAGH
S90:	STA	A,CRES			;COLLISION RESET
;
;ACCELERATION
;
	LDA	Z,FRAME
	LSR
	BCC	S130
	LSR
	BCC	S130
	JSR	A,GETAC			;GET ACCELERATION
	STA	Z,TEMP2
	CMP	I,371			;ACCELERATION.LE.SKID VALUE
	BPL	S10			;NO
	STA	A,SK+1			;YES, TURN ON SKID
S10:	LDA	Z,TEMP2
	CLC
	ADC	ZX,VEL
	BNE	S131
	LDA	I,1
S131:	STA	ZX,VEL			;VEL=VEL+ACCELERATION
;
;ENGINE RPM SOUND
;
S130:	LDA	ZX,VEL
	LSR				;GET RPM SOUND
	LSR
	LSR
	STA	Z,TEMP1
	LDY	Z,GEAR
	LDA	Z,GEAR
	ASL
	STA	Z,TEMP2
	LDA	AY,RATIO
	TAY
	LDA	I,0
S101:	CLC
	ADC	Z,TEMP1
	DEY
	BNE	S101
	LSR
	LSR
	LSR
	SEC
	SBC	Z,TEMP2
	BPL	S102
	LDA	I,0
S102:	CMP	I,20
	BMI	S104
	LDA	I,17
S104:	STA	Z,SOUND
	TXA
	ASL
	TAY
;
;UPDATE DIRECTION & STEERING
;
	JSR	A,STEER
;
;DIRECTION UPDATE
;
	LDA	ZX,VEL
	EOR	I,377
	LSR
	LSR
	LSR
	LSR
	LSR
	LSR
	STA	Z,TEMP3
	CLC
	ADC	I,3
	STA	Z,TEMP1			;CONSTANT = COM(VEL(3MSB))+3
	LDA	AY,ROTATE
	AND	I,370
	STA	Z,TEMP2
	LDA	ZX,DIRECT
	AND	I,370
	SEC
	SBC	Z,TEMP2
	BEQ	S20
	BMI	S211
	CMP	I,20
	BMI	S,+5
	STA	A,SK+1			;TURN ON SKID
	LDA	I,0
	SEC
	SBC	Z,TEMP1
	STA	Z,TEMP1			;CONSTANT=-CONSTANT
	JMP	A,S21
S211:	CMP	I,370
	BPL	S,+5
	STA	A,SK+1			;TURN ON SKID
S21:	LDA	Z,TEMP1
	CLC
	ADC	ZX,DIRECT
	STA	ZX,DIRECT		;UPDATE DIRECTION
;
;UPDATE POSITION
;
S20:	JSR	A,UPDATE		;UPDATE USER CAR POSITION
;
;CHECKPOINTS
;
S250:	LDA	Z,TRKN
	ASL
	TAX
	LDA	AX,TRKTAB		;GET CORRECT TRACK CHECKPOINTS
	STA	Z,TEMP2
	LDA	AX,TRKTAB+1
	STA	Z,TEMP2+1
	LDA	A,SLSD
	LSR
	AND	I,7
	ASL
	TAY
	LDA	NY,TEMP2
	STA	Z,TEMP4
	INY
	LDA	NY,TEMP2
	STA	Z,TEMP4+1
	LDX	Z,TEMP1
	LDA	ZX,VPOST
	AND	I,360
	STA	Z,TEMP5
	LDA	ZX,HPOST
	LSR
	LSR
	LSR
	LSR
	AND	I,17
	ORA	Z,TEMP5
	STA	Z,TEMP5
	LDY	I,0
S27:	LDA	NY,TEMP4		;GET CORRECT CHECKPOINT
	CMP	Z,TEMP5			;CAR ON CHECKPOINT?
	BEQ	S26			;YES
	INY
	CPY	I,4
	BNE	S27
	JMP	A,S290
;
;SCORE
;
S26:	LDA	A,SLSD			;SCORE=SCORE+2
	CLC
	ADC	I,2
	CMP	I,272
	BNE	S35
	LDA	A,SMSD
	CLC
	ADC	I,1
	CMP	I,272
	BNE	S266
	LDA	A,SHSD
	CLC
	ADC	I,1
	STA	A,SHSD
	LDA	I,260
S266:	STA	A,SMSD
	LDA	I,260
S35:	STA	A,SLSD
;
;TRACK UPDATE
;
	CMP	I,260			;CHECKPOINT=FINISH LINE?
	BNE	S290
	BIT	A,SPARE
	BVS	S36
	LDA	Z,TOGGLE		;YES, 2 TIMES THROUGH CURRENT TRACK?
	BNE	S36
	INC	Z,TOGGLE		;NO, BUMP TOGGLE
	JMP	A,S290
S36:	LDA	I,0
	STA	Z,TOGGLE
	INC	Z,TRKN			;YES, BUMP TRACK NUMBER
	LDA	Z,TRKN
	CMP	I,12.
	BNE	S29
	LDA	I,6
	STA	Z,TRKN
S29:	JSR	A,TRACK			;GET NEXT TRACK
;
;EXTENDED PLAY
;
S290:	LDA	Z,FLAGZ
	BEQ	S2900
	LDA	Z,FRAME
	AND	I,40
	BEQ	S,+10.
	LDX	I,2
	JSR	A,BLANK
	JMP	A,S2900
	LDX	I,2
	LDA	I,0
	JSR	A,MESS
S2900:	LDA	A,SHSD
	AND	I,77
	CMP	Z,EXTEND
	BMI	S37
	LDA	A,SMSD
	AND	I,77
	CMP	Z,EXTEND+1		;EXTENDED PLAY?
	BMI	S37
	LDA	Z,FLAGZ
	BNE	S37
	LDA	Z,FLAGQ
	BNE	S37
	INC	Z,FLAGQ
;
;FINISHED?
;
;
;TIMER
;
S37:	DEC	Z,TCOUNT		;TIME TO UPDATE TIME COUNT?
	BNE	S252			;NO
	LDA	Z,TSP
	BNE	S370
	INC	Z,TSP
	LDA	I,40
	STA	A,THSD
	LDA	I,271
	STA	A,TMSD
	LDA	I,272
	STA	A,TLSD
	JMP	A,S251
S370:	LDA	A,TLSD			;YES
	CMP	I,260			;LSD OF TIME=0?
	BNE	S251			;NO
	LDA	A,TMSD			;YES
	CMP	I,260			;MSD OF TIME=0?
	BEQ	S2500			;YES, BYPASS TIME UPDATE
	DEC	A,TMSD			;NO, DECREMENT MSD
	LDA	I,272
	STA	A,TLSD			;SET LSD=9
S251:	DEC	A,TLSD			;DECREMENT LSD
	LDA	Z,TSAVE
	STA	Z,TCOUNT
S252:	JMP	A,S50
S2500:	LDA	Z,FLAGQ
	BEQ	S2501
	LDA	I,0
	STA	Z,FLAGQ
	INC	Z,FLAGZ
	LDA	I,263
	STA	A,TMSD			;SET TIMER TO 30
	LDA	I,0
	LDX	I,2
	JSR	A,MESS
	LDA	Z,TSAVE
	STA	Z,TCOUNT
	JMP	A,S50
S2501:	STA	A,AMODE+1		;TURN OFF SOUND
	STA	A,SK			;TURN OFF SKID
	LDA	I,0
	STA	Z,CRASH			;TURN OFF CRASH
	STA	Z,SOUND			;LOWER RPM
	LDX	I,2
	JSR	A,BLANK
	LDX	I,12.
	JSR	A,BLANK
	LDA	I,0
	LDX	I,12.
	JSR	A,MESS
	LDA	I,0
	LDX	I,22.
	JSR	A,MESS
	LDA	A,SHSD
	AND	I,17
	ASL
	ASL
	ASL
	ASL
	STA	Z,TEMP1
	LDA	A,SMSD
	AND	I,17
	ORA	Z,TEMP1
	STA	Z,TEMP1
	LDA	Z,TEMP1
	CMP	Z,BCD1
	BMI	B10
	LDX	I,24.
	BPL	B20
B10:	CMP	Z,BCD2
	BMI	B11
	LDX	I,26.
	BPL	B20
B11:	LDX	I,28.
B20:	LDA	I,1
	JSR	A,MESS
	LDA	Z,CREDIT
	BNE	S,+5
	STA	A,SBO
	JSR	A,SCR
;
;FREEZE SCREEN
;
	LDA	I,10			;FREEZE SCREEN FOR 10 SECONDS
	LDX	I,177
	LDY	I,177
S40:	STA	Z,TEMP3
	STX	Z,TEMP4
	STY	Z,TEMP5
	JSR	A,COIN			;LOOK FOR COINS
	LDA	Z,COINS
	BNE	S401
	LDA	Z,CREDIT		;ANY CREDIT?
	BEQ	S400			;NO
	STA	A,SBO+1
	BIT	A,SBI			;YES, START BUTTON PRESSED?
	BPL	S401			;YES, GO ON
S400:	LDA	Z,TEMP3			;NO
	LDX	Z,TEMP4
	LDY	Z,TEMP5
	DEY
	BNE	S40
	STA	Z,TEMP5
	STX	Z,TEMP5+1
	INC	Z,FRAME
	LDA	Z,FRAME
	AND	I,40
	BEQ	S,+10.
	LDX	I,22.
	JSR	A,BLANK
	JMP	A,S9000
	LDA	I,1
	LDX	I,22.
	JSR	A,MESS
S9000:	LDA	Z,TEMP5
	LDX	Z,TEMP5+1
	STA	A,TREST
	LDY	I,177
	DEX
	BNE	S40
	LDY	I,177
	LDX	I,177
	CMP	I,11
	BCC	S,+4
	LDA	I,5
	SEC
	SBC	I,1
	BPL	S40
	LDA	I,1
	STA	Z,TEMP5
S401:	LDX	I,22.
	JSR	A,BLANK
	LDX	I,12.
	JSR	A,BLANK
	LDA	Z,CREDIT		;ANY CREDIT?
	BNE	S41
	STA	A,SBO			;TURN OFF START LIGHT
	RTS
S41:	JMP	A,S1
S50:	JSR	A,STEER
S501:	BIT	A,COINAD
	BPL	S502
	BVS	S503
S502:	JSR	A,COIN
S503:	BIT	A,AMODE			;WAIT FOR V RESET
	BVC	S501
	JSR	A,CHASC
	STA	A,TREST			;PULSE WATCHDOG
	JMP	A,PLAY
;
;
;SUBROUTINES
;
;
CNTRL:	JSR	A,AUTO			;GET AUTO-DIRECTION
	LDA	ZX,VEL
	LSR
	LSR
	LSR
	LSR
	LSR
	CLC
	ADC	I,1
	STA	Z,TEMP1			;SET ACCEL=(3 MSB OF VEL)+1
	LDY	I,3
	LDA	ZX,DIRECT
	SEC
	SBC	ZX,AUDY			;DIRECT-AUDY
	AND	I,370
	BEQ	A6
	BMI	A7
	LDY	I,375
A7:	TYA
	CLC
	ADC	ZX,DIRECT
	STA	ZX,DIRECT
	STX	Z,TEMP3
	ASL	Z,TEMP3
	LDY	Z,TEMP3
	STA	AY,ROTATE
	LDA	I,377
	STA	Z,TEMP1			;SET ACCELERATION=-1
A6:	LDA	ZX,VEL
	CLC
	ADC	Z,TEMP1			;VEL=VEL+ACCELERATION
	CMP	AX,MAXVEL
	BCC	A10
	LDA	AX,MAXVEL
A10:	STA	ZX,VEL
	JSR	A,UPDATE		;UPDATE DIRECTION
	RTS
AUTO:	LDA	I,0
	STA	Z,TEMP1
	LDA	I,04
	STA	Z,TEMP1+1
	LDA	ZX,HPOST
	CLC
	ADC	I,4			;ADD 4 TO GET CENTER OF CAR
	LSR
	LSR
	LSR				;DIVIDE BY 8
	EOR	I,37			;COMPLEMENT IT
	CLC
	ADC	Z,TEMP1
	STA	Z,TEMP1
	LDA	I,0
	STA	Z,TEMP2
	ADC	Z,TEMP1+1
	STA	Z,TEMP1+1
	LDA	ZX,VPOST
	CLC
	ADC	I,4			;ADD 4 TO GET CENTER OF CAR
	ORA	I,7			;OR IN 3 LSB'S
	EOR	I,377			;COMPLEMENT IT
	ASL
	ROL	Z,TEMP2			;SAVE CARRY
	ASL
	ROL	Z,TEMP2			;SAVE 2 MSB'S
	CLC
	ADC	Z,TEMP1
	STA	Z,TEMP1
	LDA	Z,TEMP2
	ADC	Z,TEMP1+1
	STA	Z,TEMP1+1
	LDY	I,0
	LDA	NY,TEMP1		;GET SQUARE
	BPL	AU1
	ASL
	ASL
	ASL
	ASL
	ASL
	STA	ZX,AUDY			;SAVE AUTO-DIRECTION
AU1:	RTS
UPDATE:	STX	Z,TEMP1
	LDA	I,0
	STA	Z,LFLAG
	LDA	ZX,VEL
	BEQ	U3
	LSR
	LSR
	LSR
	STA	Z,TEMP2			;MULTIPLICAND (VEL)
U1:	LDA	ZX,DIRECT
	LSR
	LSR
	LSR
	TAX
	STX	Z,TEMP3
	LDA 	AX,HOFF
	BEQ	U2
	ASL
	TAX				;MULTIPLIER (HOFF)
	JSR	A,MULT			;GET VEL*HOFF
	LDX	Z,TEMP1
	LDA	Z,TEMP4
	CLC
	ADC	ZX,HPOSL
	STA	ZX,HPOSL
	LDA	Z,TEMP4+1
	ADC	ZX,HPOST
	BPL	U5
	CMP	I,374
	BMI	U4
	LDA	I,1
	STA	Z,LFLAG
	LDA	I,0
	BEQ	U5
U4:	CMP	I,367
	BMI	U5
	LDA	I,1
	STA	A,LFLAG
	LDA	I,367
U5:	STA	ZX,HPOST
U2:	LDX	Z,TEMP3
	LDA	AX,VOFF
	BEQ	U3
	ASL
	TAX				;MULTIPLIER (VOFF)
	JSR	A,MULT			;GET VEL*VOFF
	LDX	Z,TEMP1
	LDA	Z,TEMP4
	CLC
	ADC	ZX,VPOSL
	STA	ZX,VPOSL
	LDA	Z,TEMP4+1
	ADC	ZX,VPOST
	BPL	U6
	CMP	I,350
	BMI	U7
	LDA	I,1
	STA	Z,LFLAG
	LDA	I,350
	BMI	U7
U6:	CMP	I,40
	BPL	U7
	LDA	I,1
	STA	Z,LFLAG
	LDA	I,40
U7:	STA	ZX,VPOST
U3:	LDX	Z,TEMP1
	CPX	I,0
	BEQ	U10
	LDA	Z,LFLAG
	BEQ	U10
	LDA	I,40			;IF CAR OFF TRACK,
	STA	ZX,HPOST		;RESET TO START
	LDA	I,120
	STA	ZX,VPOST
	LDA	I,300
	STA	ZX,DIRECT
	LDA	I,1
	STA	ZX,VEL
U10:	RTS
MULT:	STX	Z,TEMP2+1
	TXA
	BPL	M3
	EOR	I,377
	CLC
	ADC	I,1
	TAX
M3:	LDA	I,0
	STA	Z,TEMP4
	STA	Z,TEMP4+1
M2:	LDA	Z,TEMP4
	CLC
	ADC	Z,TEMP2
	STA	Z,TEMP4
	LDA	I,0
	ADC	Z,TEMP4+1
	STA	Z,TEMP4+1
	DEX
	BNE	M2
	LDA	Z,TEMP2+1
	BPL	M1
	LDA	I,0
	SEC
	SBC	Z,TEMP4
	STA	Z,TEMP4
	LDA	I,0
	SBC	Z,TEMP4+1
	STA	Z,TEMP4+1
M1:	RTS
INITIL:	LDX	I,0			;SET INITIAL CAR POSITIONS
	LDA	I,40
	LDY	I,0
I1:	STA	ZX,HPOST
	STY	ZX,HPOSL
	STY	ZX,VPOSL
	INX
	CPX	I,4
	BNE	I1
	LDX	I,0
	LDA	I,1
I3:	STA	ZX,VEL			;SET VELOCITIES=1
	INX
	CPX	I,4
	BNE	I3
	LDX	I,0
	LDY	I,0
	LDA	I,300
I2:	STA	ZX,DIRECT		;SET UP DIRECTIONS' ROTATION
	STA	AY,ROTATE
	INY
	INY
	INX
	CPX	I,4
	BNE	I2
	LDA	I,120
	STA	Z,VPOST
	LDA	I,104
	STA	Z,VPOST+1
	LDA	I,70
	STA	Z,VPOST+2
	LDA	I,54
	STA	Z,VPOST+3
	LDA	A,GTIME			;SET-UP GAME TIME
	EOR	I,377			;ACCORDING TO SWITCHES
	CLC
	ROL
	ROL
	ROL
	AND	I,3
	TAY
	LDA	AY,TUP
	STA	Z,TCOUNT
	STA	Z,TSAVE
	LDA	I,4			;PRO LEVEL
	STA	Z,BCD1
	LDA	I,2			;ROOKIE LEVEL
	STA 	Z,BCD2
	LDA	I,3			;EXTENDED PLAY LEVEL
	STA	Z,BCD3
	LDA	A,GTIME
	EOR	I,377
	CLC
	ROL
	ROL
	ROL
	AND	I,3
	CLC
	ADC	I,2
	STA	Z,TEMP1
	LDX	I,0
	STX	Z,TEMP2
	STX	Z,TEMP3
	STX	Z,TEMP4
TT10:	LDA	Z,TEMP2
	SED
	CLC
	ADC	Z,BCD1
	STA	Z,TEMP2
	LDA	Z,TEMP3
	CLC
	ADC	Z,BCD2
	STA	Z,TEMP3
	LDA	Z,TEMP4
	CLC
	ADC	Z,BCD3
	STA	Z,TEMP4
	CLD
	INX
	CPX	Z,TEMP1
	BNE	TT10
	LDA	Z,TEMP2
	STA	Z,BCD1
	LDA	Z,TEMP3
	STA	Z,BCD2
	LDA	Z,TEMP4
	STA	Z,BCD3
	LDA	A,BONUS			;EXTENDED PLAY
	BPL	TT11
	LDA	I,100
	STA	Z,EXTEND
	STA	Z,EXTEND+1
	LDA	Z,BCD3
	STA	Z,BCD1
	JMP	A,TT12
TT11:	LDA	Z,BCD3
	AND	I,360
	LSR
	LSR
	LSR
	LSR
	ORA	I,60
	STA	Z,EXTEND
	LDA	Z,BCD3
	AND	I,17
	ORA	I,60
	STA	Z,EXTEND+1
TT12:	LDA	I,0			;INITIALIZE SOUND & FRAME COUNT
	STA	Z,TOGGLE
	STA	Z,FRAME
	STA	A,SK
	STA	Z,SOUND
	STA	Z,SOUND+1
	STA	Z,FLAGH			;INITIALIZE FLAGS
	STA	Z,FLAGD
	STA	Z,FLAGF
	STA	Z,FLAGP
	STA	Z,FLAGQ
	STA	Z,FLAGZ
	STA	Z,TSP
	STA	A,CRASH
	STA	A,CRES			;DO A COLLISION RESET
	STA	A,STRES			;DO A STEERING RESET
	LDA	A,SLSD
	STA	Z,TEMP3
	LDA	A,SMSD
	STA	Z,TEMP3+1
	LDA	A,SHSD
	STA	Z,TEMP5+1
	LDA	I,1
	LDX	I,4			;OUTPUT "SCORE"
	JSR	A,MESS
	LDA	Z,TEMP3
	STA	A,SLSD
	LDA	Z,TEMP3+1
	STA	A,SMSD
	LDA	Z,TEMP5+1
	STA	A,SHSD
	LDA	Z,TEMP5
	BNE	I6
	LDA	I,260			;SET SCORE=000
	STA	A,SLSD
	STA	A,SMSD
	STA	A,SHSD
I6:	LDA	I,260
	STA	A,TMSD
	STA	A,TLSD			;SET TIME=100
	LDA	I,261
	STA	A,THSD
I4:	RTS
SETUP:	LDA	I,0
	STA	Z,CREDIT
	STA	Z,COINS
	STA	A,SBO
	STA	Z,TEMP5
	LDA	I,60
	STA	Z,HS1
	STA	Z,HS2
	STA	Z,HS3
	LDA	I,6
	STA	Z,CFLAG
	RTS
CHASC:	LDA	A,SLSD			;CHECK VALIDITY OF DIGITS
	AND	I,77
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,260
	STA	A,SLSD
	LDA	A,SMSD
	AND	I,77
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,260
	STA	A,SMSD
	LDA	A,SHSD
	AND	I,77
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,260
	STA	A,SHSD
	LDA	A,TMSD
	AND	I,77
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,271
	STA	A,TMSD
	LDA	A,TLSD
	AND	I,77
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,271
	STA	A,TLSD
	RTS
ASCK:	CMP	I,60
	BCC	AS1
	CMP	I,72
	BCS	AS1
	LDA	I,0
	BEQ	S,+4
AS1:	LDA	I,1
	STA	Z,TEMP1
	RTS
GETAC:	STX	Z,TEMP3
	LDA	I,0
	STA	Z,GEAR
	BIT	A,GEARAD
	BPL	G9
	INC	Z,GEAR
	BIT	A,GEARAD+1
	BPL	G9
	INC	Z,GEAR
	BIT	A,GEARAD+2
	BPL	G9
	INC	Z,GEAR
G9:	LDA	ZX,VEL
	AND	I,370
	LSR
	LSR
	TAX
	LDA	I,2
	BIT	Z,GEAR
	BEQ	G2
	INX
G2:	LDA	AX,ATAB2
	BIT	A,GP			;GAS PEDAL SET?
	BMI	G4			;NO
	LDA	AX,ATAB1
G4:	CLC
	LSR	Z,GEAR			;UPPER OR LOWER HALF BYTE?
	BCS	G6
	AND	I,360
	LSR
	LSR
	LSR
	LSR
G6:	AND	I,17
	STA	Z,TEMP2
	ROL	Z,GEAR
	LDA	I,10
	BIT	Z,TEMP2
	BEQ	G8
	LDA	Z,TEMP2
	ORA	I,360
	STA	Z,TEMP2
G8:	LDA	Z,TEMP2
	LDX	Z,TEMP3
	RTS
;
;STEERING
;
STEER:	LDX	I,0
	LDY	I,0
	LDA	Z,FLAGH
	BNE	S15
	BIT	A,STR			;ANY CHANGE IN STEERING?
	BMI	S15			;NO, BYPASS ROTATION UPDATE
	LDA	I,3
	STA	Z,TEMP1
	BIT	A,STR
	BVS	S16
	LDA	I,375
	STA	Z,TEMP1
S16:	LDA	AY,ROTATE
	CLC
	ADC	Z,TEMP1
	STA	AY,ROTATE
	STA	A,STRES			;CLEAR STEERING FLAG
S15:	RTS
;
;COIN ROUTINE
;
COIN:	JSR	A,KOIN			;GET COINS
	LDA	I,10.
	STA	Z,CFLAG
	BIT	A,MODE			;FIND COIN MODE
	BPL	C50
	BVS	C53			;2 COINS 1 CREDIT OR FREE PLAY
	LDA	I,8.
	STA	Z,CFLAG
	LDA	Z,COINS			;2 COINS, 1 PLAY
	CMP	I,2
	BEQ	S,+3
	RTS
	DEC	Z,COINS
	BPL	C53
C50:	BVS	C54
	LDA	I,6.
	STA	Z,CFLAG
C54:	LDA	Z,COINS
	BNE	S,+3
	RTS
	BVC	C53			;1 COIN 1 PLAY OR 1 COIN 2 PLAYS
	INC	Z,CREDIT
C53:	INC	Z,CREDIT
	DEC	Z,COINS
	RTS
KTEST	=0
	.IF	EQ,KTEST
;
;GET A COIN, 16MS ON MINIMUM AND 16MS OFF MINIMUM
;
KOIN:	LDA	I,9.
	STA	Z,TEMP1
	STA	Z,TEMP1+1		;INITIALIZE COUNTERS
	STA	Z,TEMP2
	STA	Z,TEMP2+1
5$:	BIT	A,COINAD		;TEST FOR MUHLAH
	BPL	10$			;YES
	BVC	10$			;YES
;
;NO COINS ON, EXIT IF DONE
;
	LDA	Z,TEMP1			;TEST ON-BOUNCE COUNTERS
	AND	Z,TEMP1+1
	CMP	I,9.
	BNE	10$			;SOMEONE IS BOUNCING
	RTS
10$:	STA	A,TREST			;WATCH-DOG
	LDX	I,1			;SET INDEX
	LDY	I,372
15$:	DEY				;1.3 MSEC DELAY
	BNE	15$
20$:	TXA				;TEST X REGISTER
	BNE	25$
	BIT	A,COINAD		;TEST COIN 1
	BVC	60$			;YES, ITS ON
	BVS	40$			;NO COIN NOW
25$:	BIT	A,COINAD		;TEST COIN 2
	BPL	60$			;GOT ONE
40$:	LDA	ZX,TEMP1		;TEST IF HAVE A COIN
	BEQ	45$			;ALREADY HAVE ONE ON
	LDA	I,9.
	STA	ZX,TEMP1		;RESET ON COUNTER (-NOISE)
	BNE	80$			;DONE
45$:	DEC	ZX,TEMP2		;OFF COUNTER TEST
	BNE	80$
	INC	Z,COINS			;INCR COIN COUNT
	LDA	I,9.
	STA	ZX,TEMP1		;RESET COUNTERS
	STA	ZX,TEMP2
	BNE	80$
60$:	LDA	I,9.
	STA	ZX,TEMP2		;RESET OFF COUNTER
	LDA	ZX,TEMP1		;TEST ON COUNTER
	BEQ	80$			;ALREADY HAVE A 16MS PULSE
	DEC	ZX,TEMP1		;ON COUNTER
80$:	DEX				;COIN INDEX
	BPL	20$			;DO NEXT COIN
	BMI	5$			;CONTINUE
	.ENDC
	.IF	NE,KTEST
KOIN:	LDA	I,9.
	STA	Z,TEMP1
	STA	Z,TEMP2
C1:	BIT	A,COINAD
	BPL	10$
	BVC	10$
	RTS
10$:	STA	A,TREST
	LDY	I,372
	DEY
	BNE	S,-1
	BIT	A,COINAD		;BIT 6 COIN TEST
	BVC	S,+8.
	LDA	I,9.
	STA	Z,TEMP1			;RESET COUNTER ON NOISE
	BPL	C2
	LDA	Z,TEMP1
	BEQ	C2
	DEC	Z,TEMP1
	BNE	C2
	INC	Z,COINS
C2:	BIT	A,COINAD		;BIT 7 COIN TEST
	BPL	S,+8.
	LDA	I,9.
	STA	Z,TEMP2			;RESET COUNTER
	BPL	C1
	LDA	Z,TEMP2
	BEQ	C1
	DEC	Z,TEMP2
	BNE	C1
	INC	Z,COINS
	JMP	A,C1
	.ENDC
MESS:	LDY	I,0
	CMP	I,0
	BEQ	S,+4
	LDY	I,200
	STY	Z,TEMP4
	LDA	AX,MESG
	STA	Z,TEMP1
	LDA	AX,MESG+1
	STA	Z,TEMP1+1
	LDA	AX,POSM
	STA	Z,TEMP2
	LDA	AX,POSM+1
	STA	Z,TEMP2+1
	LDY	I,0
M100:	LDA	NY,TEMP1
	BEQ	M200
	ORA	Z,TEMP4
	STA	NY,TEMP2
	INY
	JMP	A,M100
M200:	RTS
BLANK:	LDA	AX,POSM			;BLANK ONE LINE
	STA	Z,TEMP1
	LDA	AX,POSM+1
	STA	Z,TEMP1+1
	LDA	I,40
	LDY	I,0
B1:	STA	NY,TEMP1
	INY
	CPY	I,40
	BNE	B1
	RTS
TRACK:	STA	A,TREST
	LDA	Z,FLAGP
	BNE	T10
	LDA	Z,TRKN
	BNE	T6
	LDX	I,34.			;IF INITIAL TRACK,
T7:	JSR	A,BLANK			;BLANK AREA
	INX
	INX
	CPX	I,48.
	BNE	T7
	LDA	Z,TRKN
T6:	ASL
	TAX
	LDA	AX,TADD
	STA	Z,TEMP2
	LDA	AX,TADD+1
	STA	Z,TEMP2+1
	LDY	I,0
T1:	LDA	NY,TEMP2
	STA	AY,TH1
	INY
	CPY	I,6
	BNE	T1
T10:	CLC
	LDA	Z,TH1
	ADC	I,30
	STA	Z,TH4
	LDA	I,0
	ADC	Z,TH1+1
	STA	Z,TH4+1
	LDA	I,1
	STA	Z,FLAGP
	JSR	A,COIN
	LDY	I,0
T2:	LDA	NY,TH1
	AND	I,360
	LSR
	LSR
	LSR
	LSR
	ORA	I,240
	STA	NY,TH3
	LDA	Z,TH3
	CLC
	ADC	I,1
	STA	Z,TH3
	LDA	I,0
	ADC	Z,TH3+1
	STA	Z,TH3+1
	LDA	NY,TH1
	AND	I,17
	ORA	I,240
	STA	NY,TH3
	LDA	Z,TH1
	CLC
	ADC	I,1
	STA	Z,TH1
	LDA	I,0
	ADC	Z,TH1+1
	STA	Z,TH1+1
	LDA	Z,TH3
	CLC
	ADC	I,1
	STA	Z,TH3
	LDA	I,0
	ADC	Z,TH3+1
	STA	Z,TH3+1
	LDA	Z,TH1+1
	CMP	Z,TH2+1
	BEQ	T3
T4:	LDA	Z,TRKN
	BEQ	T2
	LDA	Z,TH1+1
	CMP	Z,TH4+1
	BEQ	T11
	BNE	T2
T3:	LDA	Z,TH1
	CMP	Z,TH2
	BNE	T4
	LDA	I,0
	STA	Z,FLAGP
	BEQ	T110
T11:	LDA	Z,TH1
	CMP	Z,TH4
	BNE	T2
T110:	JSR	A,COIN
	BIT	A,SPARE			;OIL SLICKS?
	BMI	T20
	LDA	Z,TRKN
	ASL
	ASL
	TAY
	LDX	I,0
T19:	LDA	AY,OILS
	STA	ZX,TEMP1
	INY
	INX
	CPX	I,4
	BNE	T19
	LDX	I,0
T18:	LDY	I,0
	LDA	I,74
	STA	NY,TEMP1
	LDY	I,1
	LDA	I,75
	STA	NY,TEMP1
	LDY	I,40
	LDA	I,76
	STA	NY,TEMP1
	LDY	I,41
	LDA	I,77
	STA	NY,TEMP1
	INX
	CPX	I,1
	BNE	T20
	LDA	Z,TEMP2
	STA	Z,TEMP1
	LDA	Z,TEMP2+1
	STA	Z,TEMP1+1
	JMP	A,T18
T20:	STA	A,TREST
	RTS
LINE:	LDA	A,2153
	CMP	I,313
	BNE	LIN1
	LDA	A,MES30
	CMP	I,113
	BNE	LIN1
	BIT	A,LFREQ
	BMI	S,+3
	RTS
	BIT	A,AMODE
	BPL	S,-3
	BIT	A,AMODE
	BMI	S,-3
	LDA	I,10.
LAD:	LDY	I,0
	INY
	BNE	S,-1
	SEC
	SBC	I,1
	BNE	LAD
	BMI	LIN1
	RTS
LIN1:	LDA	I,0
	STA	Z,CREDIT
	STA	Z,COINS
	RTS
SCR:	LDA	I,0
	LDX	I,32.
	JSR	A,MESS
	LDA	A,SHSD
	AND	I,77
	CMP	Z,HS3
	BMI	AAA1
	BEQ	S,+4
	BPL	AAA2
	LDA	A,SMSD
	AND	I,77
	CMP	Z,HS2
	BMI	AAA1
	BEQ	S,+4
	BPL	AAA2
	LDA	A,SLSD
	AND	I,77
	CMP	Z,HS1
	BMI	AAA1
AAA2:	LDA	A,SHSD
	AND	I,77
	STA	Z,HS3
	LDA	A,SMSD
	AND	I,77
	STA	Z,HS2
	LDA	A,SLSD
	AND	I,77
	STA	Z,HS1
AAA1:	LDA	Z,HS1
	STA	A,HLSD
	LDA	Z,HS2
	STA	A,HMSD
	LDA	Z,HS3
	STA	A,HHSD
	RTS
TRANS:	LDX	I,0
	LDY	I,0
N3:	LDA	ZX,HPOST		;UPDATE CAR POSITIONS
	STA	ZX,HPOS
	LDA	ZX,VPOST
	STA	AY,VPOS
	INX
	INY
	INY
	CPX	I,4
	BNE	N3
	RTS
;
;SIMPLE SELF-TEST
;
SLT:	LDX	I,0
	LDA	I,1
SST10:	STA	AX,2000
	STA	A,TREST
	CMP	AX,2000
	BNE	SST10
SST12:	STA	AX,2400
	STA	A,TREST
	CMP	AX,2400
	BNE	SST12
SST13:	STA	AX,3000
	STA	A,TREST
	CMP	AX,3000
	BNE	SST13
SST14:	STA	AX,3400
	STA	A,TREST
	CMP	AX,3400
	BNE	SST14
	INX
	BNE	SST10
	CLC
	ADC	I,1
	CMP	I,1
	BNE	SST10
	LDA	I,0
	STA	Z,CRASH
;
;ROM CHECKSUM TEST
;
SST15:	LDX	I,0
	LDA	I,20
	STA	Z,TEMP2
	LDA	I,5
	STA	Z,TEMP2+1
	LDA	I,0
	STA	Z,LOC
	LDA	I,40
	STA	Z,LOC+1
SST26:	LDY	I,0
	LDA	I,0
	STA	Z,TEMP3
	STA	Z,TEMP1
SST20:	LDA	Z,TEMP1
	CLC
	ADC	NY,LOC
	STA	Z,TEMP1
	INY
	BNE	SST20
	STA	A,TREST
	INC	Z,LOC+1
	INC	Z,TEMP3
	LDA	Z,TEMP3
	CMP	I,4
	BNE	SST20
SST22:	LDA	Z,TEMP1
	CMP	AX,CKSUM
	BEQ	SST25
	LDY	I,0
	TXA
	ORA	I,60
	STA	NY,TEMP2
	INC	Z,TEMP2
SST25:	INX
	CPX	I,8.
	BNE	SST26
	LDA	I,324			;WRITE OUT GAME OPTIONS
	STA	A,2152
	LDA	I,311
	STA	A,2153
	LDA	I,315
	STA	A,2154
	LDA	I,305
	STA	A,2155
	LDA	I,314
	STA	A,2314
	LDA	I,301
	STA	A,2315
	LDA	I,320
	STA	A,2316
	LDA	I,323
	STA	A,2317
	LDA	I,260
	STA	A,2161
SST30:	LDA	I,261
	BIT	A,SPARE
	BVS	S,+4
	LDA	I,262
	STA	A,2312
	LDA	I,40
	BIT	A,GTIME
	BMI	S,+4
	LDA	I,261
	STA	A,2157
	LDA	I,266
	BIT	A,GTIME
	BMI	QB1
	LDA	I,262
	BVS	S,+4
	LDA	I,265
	BMI	S,+6
QB1:	BVS	S,+4
	LDA	I,271
	STA	A,2160
	LDA	I,212
	STA	Z,TEMP1
	LDA	I,4
	STA	Z,TEMP1+1
	BIT	A,MODE
	BMI	QB2
	BVS	QB3
	LDA	I,MES6+7&377
	STA	Z,TEMP2
	LDA	I,MES6+7&177400/256.
	STA	Z,TEMP2+1
	JMP	A,QB4
QB3:	LDA	I,MES10+8.&377
	STA	Z,TEMP2
	LDA	I,MES10+8.&177400/256.
	STA	Z,TEMP2+1
	JMP	A,QB4
QB2:	BVS	QB5
	LDA	I,MES8+7&377
	STA	Z,TEMP2
	LDA	I,MES8+7&177400/256.
	STA	Z,TEMP2+1
	JMP	A,QB4
QB5:	LDA	I,304
	STA	A,2212
	LDA	I,305
	STA	A,2213
	LDA	I,315
	STA	A,2214
	LDA	I,317
	STA	A,2215
	LDX	I,0
	LDA	I,40
QB11:	STA	AX,2216
	INX
	CPX	I,12.
	BNE	QB11
	JMP	A,QB6
QB4:	LDY	I,0
QB7:	LDA	NY,TEMP2
	ORA	I,200
	STA	NY,TEMP1
	INY
	CPY	I,16.
	BNE	QB7
QB6:	BIT	A,BONUS
	BPL	QB12
	LDX	I,0
	LDA	I,40
QB13:	STA	AX,2252
	INX
	CPX	I,13.
	BNE	QB13
	JMP	QB9
QB12:	LDA	I,252
	STA	Z,TEMP1
	LDA	I,4
	STA	Z,TEMP1+1
	LDA	I,MES2+10.&377
	STA	Z,TEMP2
	LDA	I,MES2+10.&177400/256.
	STA	Z,TEMP2+1
	LDY	I,0
QB8:	LDA	NY,TEMP2
	ORA	I,200
	STA	NY,TEMP1
	INY
	CPY	I,13.
	BNE	QB8
QB9:	LDA	I,40
	BIT	A,SPARE
	BMI	S,+4
	LDA	I,74
	STA	A,2352
	BIT	A,SPARE
	BMI	S,+4
	LDA	I,75
	STA	A,2353
	BIT	A,SPARE
	BMI	S,+4
	LDA	I,76
	STA	A,2412
	BIT	A,SPARE
	BMI	S,+4
	LDA	I,77
	STA	A,2413
QB10:	BIT	A,SST
	BPL	S,+5
	JMP	A,PWRON
	STA	A,SBO
	STA	A,SK
	LDX	I,0
SL1:	LDA	AX,GEARAD
	STA	A,TREST
	BMI	S,+5
	STA	A,SK+1
	INX
	CPX	I,3
	BNE	SL1
	LDA	A,GP
	BMI	S,+5
	STA	A,SK+1
	BIT	A,COINAD
	BMI	S,+5
	STA	A,SK+1
	BVS	S,+5
	STA	A,SK+1
	BIT	A,SBI
	BMI	S,+5
	STA	A,SK+1
	JMP	A,SST30
CKSUM:	.BYTE	314,121,11,221,^H38,^H2F,^H0AC,^H46,^H30
TADD:	.WORD	20000,20446,21114
	.WORD	21562,22230,22676
	.WORD	23344,24012,24460
	.WORD	25126,25574,26242
MESG:	.WORD	MES0
	.WORD	MES2
	.WORD	MES4
	.WORD	MES6
	.WORD	MES8
	.WORD	MES10
	.WORD	MES12
	.WORD	MES14
	.WORD	MES16
	.WORD	MES18
	.WORD	MES20
	.WORD	MES22
	.WORD	MES24
	.WORD	MES26
	.WORD	MES28
	.WORD	MES30
	.WORD	MES32
POSM:	.WORD	PF+240
	.WORD	PF+100
	.WORD	PF+40
	.WORD	PF+1540
	.WORD	PF+1540
	.WORD	PF+1540
	.WORD	PF+1540
	.WORD	PF+340
	.WORD	PF+240
	.WORD	PF+1540
	.WORD	PF+340
	.WORD	PF
	.WORD	PF+1565
	.WORD	PF+1565
	.WORD	PF+1565
	.WORD	PF+153
	.WORD	PF+56
	.WORD	PF+100
	.WORD	PF+140
	.WORD	PF+200
	.WORD	PF+240
	.WORD	PF+300
	.WORD	PF+340
	.WORD	PF+400
RATIO:	.BYTE	14,10,6,5		;GEAR RATIOS FOR RPM SOUNDS
TUP:	.BYTE	46,70,113,136
MAXVEL:	.BYTE	240,220,200,160
MES0:	.ASCII	/       YOUR CAR IS /
	.BYTE	327,310,311,324,305,40,40,40,40,40,0
MES2:	.ASCIZ	/          EXTENDED PLAY/
MES4:	.ASCIZ	/  SCORE              TIME /
MES6:	.ASCIZ	/        1 COIN PER GAME   /
MES8:	.ASCIZ	/       2 COINS PER GAME   /
MES10:	.ASCIZ	/        2 GAMES PER COIN  /
MES12:	.ASCIZ	/       RATING/
MES14:	.ASCIZ	/ BLACK CARS DRIVE AUTOMATICALLY/
MES16:	.ASCIZ	/  TRACKS CHANGE AUTOMATICALLY/
MES18:	.ASCIZ	/  EXTENDED PLAY FOR   0 POINTS  /
MES20:	.ASCIZ	/       PUSH START BUTTON      /
MES22:	.ASCIZ	/            GAME  OVER/
MES24:	.ASCIZ	/PRO/
MES26:	.ASCIZ	/ROOKIE/
MES28:	.ASCIZ	/GRANNY/
MES30:	.ASCIZ	/KEE  GAMES/
MES32:	.ASCIZ	/HIGH SCORE     /
	.REPT	37772-.
	.BYTE	0
	.ENDM
.	=37772
	.BYTE	0,60,74,60,74,60
	.END
                                                                   